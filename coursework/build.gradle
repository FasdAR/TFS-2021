// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.32"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins{
    id 'java'
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:0.40.0"
    }

    task ktlintCheck(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }
    check.dependsOn ktlintCheck

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

task cleanBuildDir(type: Delete) {
    delete rootProject.buildDir
}

ext {
    minSdkVersion = 21
    targetSdkVersion = 30
    compileSdkVersion = 30
    buildToolsVersion = "30.0.3"

    androidXCoreKtx = "1.5.0-beta03"
    androidXAppCompat = "1.2.0"
    androidMaterial = "1.3.0"
    androidXConstraint = "2.0.4"
}